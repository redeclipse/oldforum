profilebuttons = [
    guicenter [ guifont "emphasis" [
        guibutton "^fgok" [
            setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity $playerprevlweap
            cleargui 1
        ] []
        guistrut 0.5
        guispring 1
        guistrut 0.5
        if (needname) [
            guibutton "^fomain menu" [showgui main]
        ] [
            guibutton "^focancel" [cleargui 1]
        ]
    ] ]
]

profilepreview = [
    guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.5 1 1
]

playerprevinherit = 0
playerprevdisinherit = 0

newgui playerprev [
    guiheader "player preview"
    guicenter [ guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [cleargui 1] 15 1 1 ]
    guistrut 0.25
    guicenter [
        guilistx 2 [
            guitext "team: "
            teamneutraltex = $teamtex
            loop t 5 [
                push t (at "neutral alpha omega kappa sigma" $t) [
                    guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
                ]
            ]
            guistrut 1
            guitext "weapon: "
            loop w $weapidxnum [
                push w (at $weapname $w) [
                    guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevinherit [ playerprevinherit = 0; playerprevdisinherit = 1 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]

loadweaps = [
    lwa = ""
    lwl = (listlen $playerprevlweap)
    lwo = (? (> $lwl $arg1) (at $playerprevlweap $arg1) -1)
    loop lw2 $weapidxloadout [
        if (= $lw2 $arg1) [
            lwa = (? $lw2 (concat $lwa $arg2) $arg2)
        ] [
            lw3 = (? (> $lwl $lw2) (at $playerprevlweap $lw2) -1)
            if (&& $lw3 (= $arg2 $lw3)) [ lw3 = $lwo ]
            lwa = (? $lw2 (concat $lwa $lw3) $lw3)
        ]
    ]
    if (listlen $lwa) [ playerprevlweap = $lwa ]
]

newgui profile [
    guibox [ profilepreview ] [
        guicenter [ guifont "emphasis" [ guitext "player name" ] ]
        guicenter [ guifield playerprevname 24 [] -1 0 "" 0 "^fzad<enter name>" ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "colour tone" ] ]
        guicenter [
            colr = (& (>> $playerprevcolour 16) 0xFF)
            colg = (& (>> $playerprevcolour 8) 0xFF)
            colb = (& $playerprevcolour 0xFF)
            guilist [
                guibackground $playerprevcolour 1 0xFFFFFF 1 1
                guistrut 6 1
                guistrut 3
            ]
            guistrut 1
            guilist [
                loop i 3 [
                    n = [col@(at "r g b" $i)]
                    guilist [
                        guilist [
                            guistrut 30 1
                            guislider $n 0 255 [
                                playerprevcolour = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)
                            ] 0 1 (at [0xFF0000 0x00FF00 0x0000FF] $i) 1 (<< $$n (* (- 2 $i) 8))
                        ]
                        guitext (concatword "^f[" (at [0xFF0000 0x00FF00 0x0000FF] $i) "]" (at "R G B" $i))
                    ]
                ]
            ]
        ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "gender" ] ]
        guicenter [
            guiradio "male" playerprevmodel 0
            guistrut 1
            guiradio "female" playerprevmodel 1
        ]
    ] [ profilebuttons ]
    guitab "loadout"
    guibox [ profilepreview ] [
        guilist [
            //guilist [ 
            //    guibackground
            //    guicenter [
            //        guistayopen [
            //            gdw = 0
            //            gdl = (listlen $playerprevlweap)
            //            loop w2 $weapidxloadout [
            //                w3 = (? (> $gdl $w2) (at $playerprevlweap $w2) -1)
            //                hi = (mod $w2 2)
            //                al = (|| (= $w3 0) (allowedweap $w3))
            //                guilist [
            //                    guibackground (? (&& $al (< $gdw $maxcarry)) 0x303030 0x000000)
            //                    guistrut 0.25
            //                    guicenter [ guitext (format "%1%2" (? $hi "^fd" "^fw") (+ $w2 1)) ]
            //                    guistrut 0.5
            //                    guilist [
            //                        if (= $w3 0) [ guibackground 0xFFFFFF 0.01 0xFFFFFF 1 1 ]
            //                        guistrut 0.125
            //                        guiimage [textures/question] [loadweaps @w2 0] 0.75 0 [textures/blank] [] (? (= $w3 0) 0xFFFFFF 0x808080)
            //                        guistrut 0.125
            //                    ] 
            //                    loop w1 $weapidxloadout [
            //                        w4 = (+ $w1 $weapidxoffset)
            //                        w5 = (at $weapname $w4)
            //                        guilist [
            //                            if (= $w3 $w4) [ guibackground 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
            //                            guistrut 0.125
            //                            guiimage (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [loadweaps @w2 @w4] 0.75 0 [textures/blank] [] (? (= $w3 $w4) $[@[w5]colour] 0x808080)
            //                            guistrut 0.125
            //                        ] 
            //                    ]
            //                ]
            //                if $al [ gdw = (+ $gdw 1) ]
            //            ]
            //        ]
            //    ]
            //]
            guistrut 1
            randomcolour = -1
            guilist [
                guicenter [ guitext "select favourite weapons" ]
                guistrut 1 
                guilist [
                    guistayopen [
                        loop i 8 [
                            guilist [
                                wi = (at $playerprevlweap $i)
                                if (< $wi 1) [
                                    wn = random
                                    wt = textures/question
                                ] [    
                                    wn = (at $weapname $wi)
                                    wt = (? (allowedweap $wi) [textures/weapons/@[wn]] [textures/warning])
                                ]
                                guiimage $wt [
                                    playerprevlweap = (concat @wi (listdel $playerprevlweap @wi))
                                ] 1.75 (< $i $maxcarry) "" [
                                    playerprevlweap = (concat (listdel $playerprevlweap @wi) @wi)
                                ] $[@[wn]colour]
                                guicenter [ guitext $wn ]
                            ]    
                        ]    
                    ]    
                ]    
                guistrut 0.5
                guifont "little" [
                    guicenter [ guitext (format "you can carry ^fs^fc%1^fS %2" $maxcarry (? (= $maxcarry 1) "weapon" "weapons")) ]
                    guicenter [ guitext "^faif a weapon is not available, the next one in the list will be used." ]
                    guicenter [ guitext "^faclick the icons to sort the weapons according to your preference." ]
                    guicenter [ guitext "^faright click moves a weapon to the end of the list." ]
                    guicenter [ guitext "^fanew weapons are available after respawn." ]
                ]
                guistrut 1
                guifont "little" [
                    guicenter [ guicheckbox "pick a loadout every new game" showloadoutmenu ]
                    guicenter [ guistayopen [ guibutton "reset to defaults" [playerprevlweap = "3 7 5 4 6 8 2 0"]]]
                ]
                guistrut 1
            ]
        ]
    ] [ profilebuttons ]
    guitab "vanity items"
    guibox [ profilepreview ] [
        guicenter [
            guilist [
                vanityset = (listlen $playerprevvanity)
                loop z $vanityset [
                    vaint = (at $playerprevvanity $z)
                    vainn = (findvanity $vaint)
                    if (>= $vainn 0) [
                        guistayopen [ guicenter [ guibutton (format "^fy%1" (getvanity $vainn 2)) [ playerprevvanity = (listdel $playerprevvanity @(escape $vaint)) ] ] ]
                    ]
                ]
            ]
            guistrut 1
            guispring 1
            guistrut 1
            guilist [
                guifont "little" [
                    vanitynum = (getvanity)
                    guiloopsplit z 3 $vanitynum [
                        vaint = (getvanity $z 1)
                        vainn = (indexof $playerprevvanity $vaint)
                        if (< $vainn 0) [
                            guistayopen [ guicenter [ guibutton (format "^fa%1" (getvanity $z 2)) [ playerprevvanity = (concat $playerprevvanity @(escape $vaint)) ] ] ]
                        ]
                    ] [ guistrut 2 ]
                ]
            ]
        ]
    ] [ profilebuttons ]
    guitab "user account"
    guibox [ profilepreview ] [
        guicenter [ guitext "username " ]
        guicenter [ guifield accountname 48 [] ]
        guistrut 0.5
        guicenter [ guitext "private key " ]
        guicenter [ guifield accountpass 48 [] ]
        guistrut 1
        guicenter [ guicheckbox "identify on connect" authconnect ]
        guicenter [ guicheckbox "identify when playing offline" quickauthchecks ]
        guistrut 1
        guicenter [ guitext "^fyaccount applications are available at" ]
        guicenter [ guitext "^fchttp://redeclipse.net/apply" ]
        guistrut 1
    ] [ profilebuttons ]
] [
    if (= $guipasses 0) [
        if $playerprevdisinherit [ playerprevdisinherit = 0 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
        playerprevlweap = ""
        break = 0
        loopwhile i (+ 1 $weapidxloadout) [= $break 0] [
            q = (getloadweap $i)
            //if (< $q 0) [ break = 1 ] [ playerprevlweap = (? $i (concat $playerprevlweap $q) $q) ]
            playerprevlweap = (? $i (concat $playerprevlweap $q) $q) 
        ]
    ]
]
