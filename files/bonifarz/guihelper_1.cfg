
filename = "mygui.cfg"
boxwidth = 55
boxlines = 5
showpreview = 0
dir = "home"
subdirs = 0
ext = "cfg"
k = 0

getfiles = [
    files = ""
    loopfiles i $dir $ext [
        append files $i
    ]
    if ($subdirs) [
        loopfiles i $dir "" [
            if (= (stringstr $i ".") -1) [
                loopfiles j (format "%1/%2" $dir $i) $ext [
                    append files (format "%1/%2" $i $j)
                ]
            ]
        ]
    ]
]
getfiles 

newgui browser [
    guistayopen [
        guilist [
            guifont super [ guitext "listing all "]
            guifield ext 3 [getfiles]
            guifont super [ guitext " files in "]
            guifield dir 12 [getfiles]
        ]    
        guistrut 1
        guilist [
            guilist [
                loop j 10 [
                    i = (at $files (+ $j $k))
                    guibutton $i [
                        filename = (format "%1/%2.%3" $dir @i $ext)
                        cleargui 1
                    ]
                ]
            ]
            guispring
            guislider k 0 (- (listlen $files) 10) [] 1 1 
        ]
        guistrut 1
        guicheckbox "show files in subdirectories" subdirs 1 0 [getfiles ; k = 0]
    ]
]    

newgui guihelper [
    guistayopen [
        guistrut 0.5
        guilist [
            guifield filename 30
            guistrut 1
            guibutton "load" [textfocus @filename; textload @filename]
            guistrut 1
            guibutton "browse" [showgui browser]
            guistrut 1
            guibutton "save" [textfocus @filename; textsave @filename]
            guistrut 1
            guibutton "exec" [exec $filename]
        ]
        guistrut 0.5
        guilist [
            guilist [
                guieditor $filename (* -1 $boxwidth) $boxlines
                guislider boxwidth 55 100
            ]        
            textinit $filename $filename
            guislider boxlines 1 30 [] 1
        ]
        guistrut 1.5
        guilist [
            guispring 1
            guilist [
                guistrut 20 1
                guitext "tips per line:" textures/info 
                guistrut 0.2
                    if (> (stringlen $textcurrentline) 1) [
                    looplist j $textcurrentline [
                        looplist i $j [
                            tip = (getvardesc $i)
                            if (stringlen $tip) [
                                guibutton $i [] [] 
                            ] [    
                                //guitext $i
                            ]    
                            if (=s $guirollovername $i) [guitooltip $tip 2200]
                        ]    
                    ] [
                        guitext "^fanone"
                    ]    
                ]    
            ] 
            guilist [
                guistrut 20 1
                guibutton "copy selection" [textfocus @filename; textcopy]
                guistrut 0.2
                guibutton "copy and preview" [textfocus @filename; textcopy; showpreview = 1]
                guibutton "paste" [textfocus @filename; textpaste]
                guibutton "select all" [textfocus @filename; textselectall]
                guibutton "clear all ^fy(right click)" [textfocus @filename; textclear]
            ]
            guilist [
                guitext "show a preview below:"
                guistrut 0.2
                guiradio "none" showpreview 0
                guiradio "the copied selection" showpreview 1
                guiradio "the current line" showpreview 2
                guiradio "the entire file" showpreview 3
            ]
            guispring 1
        ]
        if ($showpreview) [
            guibar
            guilist [
                guispring
                guilist [
                    case $showpreview 1 [
                        textfocus "#pastebuffer"
                        textexec
                        textfocus @filename
                    ] 2 [
                        @textcurrentline
                    ] 3 [
                        textexec
                    ]    
                ]    
                guispring
            ]    
        ]
    ]    
]

showgui guihelper
