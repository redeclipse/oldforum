//////////////////////////////////////////////////
// Weathermenu 0.1 by Ricky Thomson 'unixfreak' //
//////////////////////////////////////////////////

// Save script as 'weather.cfg' in your RE folder,
// load it with autoexec.cfg by adding 'exec weather.cfg'
//
// Usage:   /weather
//
// Some entity types spawn particles below the movable object,
// If you can't see some (such as mist) try raising the particle box
// higher up above ground level.
//
// Also, you will have to manually adjust some of the particles 
// parameters if you want to fill larger area, these are only
// a guide to quickly get a specific kind of particle.
// Have Fun!

//particle type presets ///////////////
//   l= light, m= medium,  h= heavy 
///////////
//rain
weather_rl = [ newent particles 4 300 170 0x22222B 25 0 0 0 0 0 0 150; entautoview ]
weather_rm = [ newent particles 4 300 170 0x22222B 25 0 0 0 0 0 0 60; entautoview ]
weather_rh = [ newent particles 4 300 170 0x22222B 25 0 0 0 0 0 0 0; entautoview ]
////////////
//snow
weather_sl = [ newent particles 12 312 200 0xeeeeee 2000 35 6 50 30 0 0 0; entautoview ]
weather_sm = [ newent particles 12 312 200 0xeeeeee 2000 50 6 70 32 0 0 0; entautoview ]
weather_sh = [ newent particles 12 312 200 0xeeeeee 2000 80 6 100 35 0 0 0; entautoview ]
///////////
//mist
weather_ml = [ newent particles 9 268 68 0x222222 1000 2000 0 1 8 0 0 100; entautoview ]
weather_mm = [ newent particles 9 268 68 0x222222 1000 2000 0 1 13 0 0 100; entautoview ]
weather_mh = [ newent particles 9 268 68 0x222222 1000 2000 0 1 24 0 0 100; entautoview ]
////////////
//lightning
weather_ll = [ newent particles 7 268 362 0xddaaff 30 100 0 0 0 0 0 2500; entautoview ]
weather_lm = [ newent particles 7 268 362 0xddaaff 30 800 0 0 0 0 0 1000; entautoview ]
weather_lh = [ newent particles 7 268 362 0xddaaff 30 800 0 0 0 0 0 0; entautoview ]
/////////
//blizzard
weather_bl = [ 	newent particles 12 333 200 0xeeeeee 2000 70 6 81 92 0 0 0;
		newent particles 9 333 200 0x222222 4000 1000 0 81 150 0 0 0; entautoview ]
weather_bm = [  newent particles 12 333 200 0xeeeeee 2000 120 6 81 92 0 0 0;
		newent particles 9 333 200 0x222222 4000 2000 0 81 150 0 0 0; entautoview ]
weather_bh = [  newent particles 12 333 200 0xeeeeee 2000 252 6 81 92 0 0 0;
		newent particles 9 333 200 0x222222 4000 4000 0 81 150 0 0 0; entautoview ]

///////////////
//dust
weather_dh = [ newent particles 9 333 275 16750882 4000 8000 0 10 240 0 2 0; entautoview ]
//////////////
//acid
weather_ah = [
	newent particles 13 310 335 16777215 1000 220 0 48 11 0 3 0;
	newent particles 13 312 335 1677215 1000 220 0 -82 11 0 3 0;
	newent particles 13 332 304 16777215 2000 209 0 -77 35 0 2 0;
	newent particles 13 311 335 2236962 1000 1000 0 61 11 0 3 100;
	newent particles 7 301 304 2236962 0 80 0 3 35 0 3 0;
	newent particles 7 302 304 2236962 0 80 0 3 35 0 3 0;
	newent particles 3 380 1677215 0 3 0 0 0 0 0 0; entautoview;
]
//////////////
//volcanic ash
weather_vl = [  newent particles 8 333 200 16737826 2000 70 2 81 92 0 1 0;
		newent particles 9 333 200 2236962 4000 1000 0 81 150 0 0 0; entautoview ]
weather_vm = [  newent particles 8 333 200 16737826 2000 120 2 81 92 0 1 0;
		newent particles 9 333 200 2236962 4000 2000 0 81 150 0 0 0; entautoview ]
weather_vh = [  newent particles 8 333 200 16737826 2000 252 2 81 92 0 1 0; 
		newent particles 9 333 200 2236962 4000 4000 0 81 150 0 0 0; entautoview ]
/////////////
//ambience
weather_aml = [	newent particles 11 376 900 1118481 10000 91 0 -5 2 0 0 0
		newent particles 11 376 900 1118481 10000 91 0 5 -4 0 0 0;entautoview 
]
/////////////
//confetti
weather_cl = [ newent particles 11 312 200 0xffffff 2000 35 6 50 30 0 3 0; entautoview ]
weather_cm = [ newent particles 11 312 200 0xffffff 2000 50 6 70 32 0 3 0; entautoview ]
weather_ch = [ newent particles 11 312 200 0xffffff 2000 80 6 100 35 0 3 0; entautoview  ]

// Environment Presets ////////////
weather_env_sand = [
	skybox skyboxes/yellow
	fogdomemax 1
	fogcolour 180 80 40
	fog 600
	weather_dh
]

weather_env_cold = [
	skybox skyboxes/grey
	fog 500 
	fogdomemax 1
	fogcolour 255 255 255
	weather_bm 
]

weather_env_hell  = [
	skybox mayhem/flame
	weather_vm
	fog 500
	fogdomemax 1
	fogcolour 0 0 0
	
]


	
// gui object template
// EG: 
//    weather_pl = [ newent particles ... ... ]
//    weather_ph = [ newent particles ... ... ]
//	...
//    weather_item [Poop] [pl] [null] [ph] 
//

weather_item = [
		guibar
		guilist [
			guifont "little" [
				guitext $arg1
				guispring 1
				guistrut 0.5; guibar; guistrut 0.5
				// can pass 'null' , might not need light/medium/heavy for all particle types
				if (!= (strcmp $arg2 null) 1) [ 
					guibutton "^fgLight" [ (concatword weather_ @arg2) ]
					guistrut 0.5; guibar; guistrut 0.5 
				]
				if (!= (strcmp $arg3 null) 1) [ 
					guibutton "^fyMedium" [ (concatword weather_ @arg3) ]
					guistrut 0.5; guibar; guistrut 0.5
				]
				if (!= (strcmp $arg4 null) 1) [
					guibutton "^frHeavy" [ (concatword weather_ @arg4) ]
		  			guistrut 0.5
				]
			]
		]
]

weather_env_item = [
		guibar
		guilist [
			guifont "little" [
				guitext $arg1
				guispring 1
				guistrut 0.5; guibar; guistrut 0.5
				guibutton "^foLoad" [ (concatword weather_env_ @arg2) ]
		  		guistrut 0.5

			]
		]
]
newgui weathermenu [
	guiheader Particles
	guistrut 50 1
	guistayopen [
		guitext "Weather Effects"
		guistrut 0.2
		weather_item [Rain] 		[rl] [rm] [rh]
		weather_item [Snow] 		[sl] [sm] [sh]
		weather_item [Mist] 		[ml] [mm] [mh]
		weather_item [Lightning] 	[ll] [lm] [lh]
		weather_item [Blizzard] 	[bl] [bm] [bh]


		guibar

		guistrut 2
		guitext "Fun/Misc. Effects"

		weather_item [Acid] 		[null] [null] [ah]
		weather_item [Volcanic Ash]   	[vl] [vm] [vh]
		weather_item [Confetti]		[cl] [cm] [ch]
		weather_item [Dust] 		[null] [null] [dh]
		weather_item [Ambience]		[null] [null] [aml]

		guibar
		guistrut 2
		guilist [
			guifont "emphasis" [
				guibutton "^foCancel entity placement" [ delent ]
			]
		]
	]





	guitab Environment
		guitext "Environment Presets" // Fogdome, cloudlayer,  cloudbox, skybox, etccc..
		guistrut 0.2
		weather_env_item [Desert Storm] [sand]
		weather_env_item [Snow Storm] [cold]
		weather_env_item [Fire Storm] [hell]
		
		guibar
		guistrut 2
		guifont "little" [
				guitext "Warning, these override skybox, fog, etc"
		]

	guitab About
	guistrut 0.2
	
		guilist [
			guifont "emphasis" [
				guitext "^fgWeather menu ^fw0.1" 
			]
		]
		guistrut 2
		guilist [
			guitext "^fyExamples of weather systems using particle entitys"
		]
		guistrut 0.2
		guilist [
			guitext "by Ricky Thomson '^frunixfreak^fw'"
		]
		guistrut 0.8
		guilist [
			guifont "little" [
				guitext "Tip; If you cannot see any particles, try moving the entity."
			]
		]

]

weather = [ if (= $editing 1) [ showgui weathermenu ] [ echo "^fzyr Weathermenu: Need to be in editmode!" ] ]


